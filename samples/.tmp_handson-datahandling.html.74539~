<p>
	In this hands on we will do a simple database call and output the data
</p>

<p>
	<ol>
		<li>
			Open up /www/resume.cfm in your code editor
		</li>
		<li>
			On line 1 replace the <cfset> tage with an open <cfquery> tag with the following attributes:
			<ul>
				<li>
					Name = mySkillSet
				</li>
				<li>
					Datasource = Learncfinaweek
				</li>
			</ul>
		</li>
		<li>
			On the next line enter the following SQL code:
<pre>
SELECT
	name
FROM
	skillset
ORDER BY
	name DESC
</pre>
		</li>
		<li>
			On the next line write a closing </cfquery> tag
		</li>
		<li>
			Your code should look similar to this:
<pre>
<cfquery name="mySkillset" datasource="learncfinaweek">
	SELECT
		name
	FROM
		skillset
	ORDER BY
		name DESC
</cfquery>		
</pre>			
		</li>
		<li>
			Please note that the datasource you are using was created during the initial set up process for you.
		</li>
		<li>
			Locate the opening <cfloop> tag on or around line 120 and replace it’s attributes with:
			<ul>
				<li>
					Query = mySkillset
				</li>
			</ul>
		</li>
		<li>
			Inside the <cfloop> tag replace all references to skill with #mySkillset.name#
		</li>
		<li>
			Your code should look similar to this:
<pre>
<cfloop query="mySkillset">
	<li class="#mySkillset.name#" id="#mySkillset.name#">#mySkillset.name#</li>
</cfloop>	
</pre>			
		</li>
		<li>
			Open up your browser and navigate to /www/resume.cfm and confirm that the Skills are still displaying.
		</li>
	</ol>
</p>

<p>
	In this hands on we are going to create a more complex query and output that to the page.
</p>

<p>
	<ol>
		<li>
			Open up /www/resume.cfm in your code editor
		</li>
		<li>
			Bellow the mySkillset query create another open <cfquery> tag with the following attributes:
			<ul>
				Name = myResume
			</ul>
			<ul>
				Datasource = learncfinaweek
			</ul>
		</li>
		<li>
			After the opening <cfquery> tag provide the following SQL text: 
			<pre>
SELECT
    title, 
    startDate, 
    endDate, 
    details, 
    type
FROM
    resume
ORDER BY
    type, 
    endDate, 
	startDate 
			</pre>
		</li>
		<li>
			After the SQL text, add a close </cfquery> tag.
		</li>
		<li>
			Your code should look similar to this:
<pre>
<cfquery name="myResume" datasource="learncfinaweek">
	SELECT
	    title, 
	    startDate, 
	    endDate, 
	    details, 
	    type
	FROM
	    resume
	ORDER BY
	    Type DESC, 
	    endDate, 
	    startDate
</cfquery>	
</pre>
		</li>
		<li>
			Locate the Chapter 4 – Section 2 comment tag on or around line 90.
		</li>
		<li>
			After the comment add a <cfoutput> tage with the following attributes:
			<ul>
				<li>
					Query = myResume
				</li>
				<li>
					Group = type
				</li>
			</ul>
		</li>
		<li>
			Add a closing </cfoutput> tag after the first <p> tag on or around line 116.
		</li>
		<li>
			Delete the lines of code from after the closing </cfoutput> tag to the second </div> tag on or around line 130. 
		</li>
		<li>
			Your code should look similar to this:
<pre>
<div class="left">
	<!-- Chapter 4 - Section 2 -->
	<cfoutput query="myResume" group="type">
		<h2>Work Experience <span>&nbsp;</span></h2>
		<h5>Senior Developer - Google Inc <span>2010 to present </span> </h5>
		<p>Lorem ipsum dolor sit amet, habitasse pretium dolor sociis. Nulla et facilisis interdum elit amet erat, consectetuer condimentum eaque, ante maecenas Suspendisse libero diam.</p>
	</cfoutput>
</div>	
</pre>
		</li>
		<li>
			Replace the text “Work Experience” with #myResume.type#
		</li>
		<li>
			After that line place an opening <cfoutput> tag.
		</li>
		<li>
			Note that we are using the group functionality within ColdFusion with allows sub loops to be performed inside of a <cfoutput>.  This is the only time that a <cfoutput> ta should be nested inside of another <cfoutput> tag.
		</li>
		<li>
			Place a closing </cfoutput> tag just before the other </cfoutput> closing tag. 
		</li>
		<li>
			Your code should loosk similar to this:
<pre>
<div class="left">
	<!-- Chapter 4 - Section 2 -->
	<cfoutput query="myResume" group="type">
		<h2>#type# <span>&nbsp;</span></h2>
		<cfoutput>
			<h5>Senior Developer - Google Inc <span>2010 to present </span> </h5>
			<p>Lorem ipsum dolor sit amet, habitasse pretium dolor sociis. Nulla et facilisis interdum elit amet erat, consectetuer condimentum eaque, ante maecenas Suspendisse libero diam.</p>
		</cfoutput>
	</cfoutput>
</div>
</pre>			
		</li>
		<li>
			Replace the “Senior Developer –Google Inc” text with #myResume.title#
		</li>
		<li>
			Replace the “2010 to present” text with #myResume.startDate# to #myResume.endDate#
		</li>
		<li>
			Replace the contents of the <p> tag with #myResume.details#
		</li>
		<li>
			Your code should look similar to this:
<pre>
<div class="left">
	<!-- Chapter 4 - Section 2 -->
	<cfoutput query="myResume" group="type">
		<h2>#type# <span>&nbsp;</span></h2>
		<cfoutput>
			<h5>#myResume.title# <span>#myResume.startDate# to #myResume.endDate# </span> </h5>
			<p>#myResume.details#</p>
		</cfoutput>
	</cfoutput>
</div>
</pre>			
		</li>
		<li>
			Open up a browser and navigate to /www/resume.cfm
		</li>
		<li>
			Note that there are new Work Experience and Education information is displaying.  Also note that the dates are not displaying in a nicely readable format.
		</li>
		<li>
			Go to the myResume.startDate output on or around line 116 and change it to read: #dateFormat(myResume.startDate,”mm/dd/yyyy”)#
		</li>
		<li>
			Reload the /www/resume.cfm page in the browser and note that the start dates are now in a nicely readable format.
		</li>
		<li>
			Go to the myResume.endDate output on or around line 116 and make the same update.  The code should read: #dateFormat(myResume.endDate,"mm/dd/yyyy")#
		</li>
		<li>
			Reload /www/resume.cfm page in the browser.  Notice that the date range does not show an end date for the Senior Developer position?  This is because there is no end date in the database.  Let’s make that a bit more readable.
		</li>
		<li>
			Go back to the reference to myResume.endDate on or around line 116.
		</li>
		<li>
			After the word “to” and before the start of the dateformat function add the following code:
<pre>
<cfif len(myResume.endDate)>
</pre>			
		</li>
		<li>
			After the end of the dateFormat function add the following code:
<pre>
<cfelse>present</cfif>	
</pre>			
		</li>
		<li>
			Your code should look similar to the following:
<pre>
<div class="left">
	<!-- Chapter 4 - Section 2 -->
	<cfoutput query="myResume" group="type">
		<h2>#type# <span>&nbsp;</span></h2>
		<cfoutput>
			<h5>#myResume.title# <span>#dateFormat(myResume.startDate,"mm/dd/yyyy")# to <cfif len(myResume.endDate)>#dateFormat(myResume.endDate,"mm/dd/yyyy")#<cfelse>present</cfif> </span> </h5>
			<p>#myResume.details#</p>
		</cfoutput>
	</cfoutput>
</div>
</pre>			
		</li>
		<li>
			Reload /www/resume.cfm in your browser.
		</li>
		<li>
			You should now see that the Senior Developer entry has a more readable output.
		</li>
	</ol>
</p>

<h2>
	Homework
</h2>
<p>
	Update the Blog and Portfolio sections to pull the data from the database
</p>

<p>
	In this hands on we are going to generate an xml sitemap that if formatted for use with Google Sitemaps.
</p>
<p>
	<ol>
		<li>
			Create a new file in the root of your site called generateSitemap.cfm
		</li>
		<li>
			On line 1 create an open <cfxml> tage and provide the following attributes:
			<ul>
				<li>
					Variable = xmlSiteMap
				</li>
			</ul>
		</li>
		<li>
			After the opening <cfxml> tag enter the following code:
<pre>
<?xml version="1.0" encoding="UTF-8"?>
<urlset xmlns="http://www.google.com/schemas/sitemap/0.90">
  <url>
    <loc>http://www.myWebsite.com/</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/about.cfm</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/resume.cfm</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/portfolio.cfm</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/contact.cfm</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/blog.cfm</loc>
    <priority>0.5</priority>
  </url>
</urlset>	
</pre>			
		</li>
		<li>
			After this code enter a closing </cfxml> tag.
		</li>
		<li>
			Your code should look similar to this:
<pre>
<cfxml variable="xmlSitemap">
<?xml version="1.0" encoding="UTF-8"?>
<urlset xmlns="http://www.google.com/schemas/sitemap/0.90">
  <url>
    <loc>http://www.myWebsite.com/</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/about.cfm</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/resume.cfm</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/portfolio.cfm</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/contact.cfm</loc>
    <priority>0.5</priority>
  </url>
  <url>
    <loc>http://www.myWebsite.com/blog.cfm</loc>
    <priority>0.5</priority>
  </url>
</urlset>	  
</cfxml>	
</pre>			
		</li>
		<li>
			After the closing </cfxml> tag type an open <cfoutput> tag.
		</li>
		<li>
			On the line after the <cfoutput> tag write #toString(xmlSiteMap)#
		</li>
		<li>
			After the toString line of code write a closing </cfoutput> tag.
		</li>
		<li>
			Open up a browser and navigate to /www/generateSiteMap.cfm
		</li>
		<li>
			View source of the page and you will see the generated XML 
		</li>
		<li>
			Return to your code editor and remove the <cfoutput> tags and their contents.
		</li>
		<li>
			After the closing </cfxml> tag create a <cffile> tag with the following attributes:
			<ul>
				<li>
					Action = write
				</li>
				<li>
					File = #expandPath(‘./sitemap.xml’)#
				</li>
				<li>
					Output = #toString(xmlSiteMap)#
				</li>
			</ul>
		</li>
		<li>
			Your code should look similar to this:
<pre>
<cffile action="write" file="#expandpath('./sitemap.xml')#" output="#toString(xmlSitemap)#" />	
</pre>			
		</li>
		<li>
			After the <cffile> tag create a <p> tag and provide the text “File created!” and place a closing </p> tag after the line of text.
		</li>
		<li>
			In your browser reload the /www/generateSiteMap.cfm file and confirm that you see the message “File Created!”
		</li>
		<li>
				Navigate to /www/sitemap.xml and confirm that the xml file was created and that all your xml nodes are displayed.
		</li>
	</ol>
</p>

